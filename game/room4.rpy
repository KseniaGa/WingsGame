# ref: https://uk.wikipedia.org/wiki/%D0%9B%D1%96%D1%81%D0%BE%D0%B2%D0%B8%D0%BA

# Лісовик уявлявся як бородатий сірий дід, часто одягнений у звірині шкури чи червоний одяг. Йому притаманні атрибути, 
# пов'язані з лівим боком — ліва частина одягу покриває праву, ліве взуття одягнуто на правій нозі тощо. Може змінювати зріст, 
# стаючи то нижчим від трави, то вищим за дерева. Очі цього духа зелені й палають[6][7]. Також лісовик здатний постати в подобі 
# вовка, сови, або вітру[8]. Вважалося, лісовик не має тіні. Влітку ходить лісом, а взимку спить у землі[7].

# Може лякати людей, що заходять до лісу, своїм сміхом, збивати зі шляху, забрати дитину. Вважався покровителем вовків та дрібних 
# звірів[6], що випасає зайців і оленів, а ведмеді служать йому як охоронці[8]. В пізніх уявленнях лісовики полюбляють грати в 
# карти і коли один лісовик програється, то віддає іншому своїх звірів[7]. Жіночий відповідник лісовика — лісунка, що уявлялася як 
# жінка з довгими грудьми[6], є дружиною лісуна-чоловіка. За уявленнями словʼян, лісовики мають своє військо й підданих, коли 
# воюють люди, між собою воюють і лісовики[7].

# За народними уявленнями слов'ян, лісовика можна задобрити, лишивши для нього частування. Наприклад, мисливцям за це дух 
# приганятиме звірів, а худобі пастухів не дає загубитися в лісі. Вважалося, лісовик не любить тих, хто лається в його володіннях, 
# хто відпочиває на стежці, заходить до лісу в певні дні. Особливо лісовики начебто активні на Іванів день, Воздвиження і Єрофія[8].

# Серед українців було повір'я, що лісовик зимує в барлозі, поруч із ведмедем, і з носа у нього стирчить бурулька. Існувало повір'я: 
# якщо смикнути полісуна за бурульку, вона розсиплеться золотими грішми[9].


default lisovyk_banana_joke = False

label room4:
    # scene room hell bg
    default lisovyk_attitude = 0 
    hide all
    scene lisovykbg

    if lisovyk_joined:
        jump lisovyk_already_joined  
    elif lisovyk_visited:
        jump lisovyk_already_visited  
    else: 
        jump lisovyk_intro


label lisovyk_already_joined:
    b "Тут уже нічого не залишилось, окрім старого сухого листя."
    call screen backButton

label lisovyk_already_visited:
    show berehynia at right with dissolve
    b "Я знаю, що він старий і буркотливий, але ти впораєшся!"

    show lisovyk at center with dissolve
    show vila at left with dissolve
    hide berehynia at right with dissolve
    show lisovyk at rightly with move 
    
    jump lisovyk_guess

label lisovyk_intro:
    show lisovyk at center with dissolve
    show vila at left with dissolve
    pause 0.1
    show berehynia at rightly with dissolve

    b "Лісовик — вояк у серці. До самого кінця захищав він природу і всіх, хто живе у прохолодних тінях лісу."
    b "Тепер він сам перебуває у глибоких тінях, темряві, куди не пробивається жоден промінь сонця. Але я впевнена, що ти зможеш допомогти і йому, Віло..."
    b "І ще, будь обережна з наступним..."
    b "Іноді його пам’ять підводить через вік, але йому не подобається, коли люди нагадують йому про це! Залишайся доброю!"

    # flashback scene about Lisovyk?

    hide berehynia at rightly with dissolve

    #$ show_progress_bar()

    l "Ой, це виглядає не дуже, це виглядає недобре, кажу вам."

    show lisovyk at rightly with move 
    v "Лісовик? Лісовик, я — Віла."

    v "Лісовик..?"

    # l "Liso-what? Speak louder, child. The voices of leaves keep whispering into my ears, I forget myself."
    l "Лісо-що? Говори голосніше, дитя. Голоси навколо шепочуть мені на вухо, я все забуваю, і себе теж."

label lisovyk_guess: 
    
    menu:
        "Роказати жарт":
            v "У тебе що, банани у вухах?"
            # "Presence of Lisovyk makes Vila feel like an actual child, even though she is hundreds years old, maybe older than him!"
            l "Перепрошую?"
            l "Банани?"
            l "Банани не ростуть там, звідки я. Про них піклується батько джунглів."
            v "Батько джунглів???"
            l "Хм. Давно вже я з ним не розмовляв... Хороший був хлопець. Якщо він існує. Не знаю, чому мені здається, що це хтось, кого я знаю."
            l "*раптова ностальгія*"
            v "*Не має уявлення, про що він говорить*"
            $ lisovyk_attitude+=5 #for sudden nostalgia
            $ lisovyk_banana_joke=True
            jump lisovyk_remind_who_he_is

        "Напряму поділитись планом втечі":
            v "Я подорожувала цими печерами вже деякий час..."
            v "І дізналася, що звідси є вихід... Але тобі потрібно згадати, хто ти є, Лісовику!!!"
            l "Бррхкмм. Що?!"
            l "Я тільки що випив ранкової роси. Або, я думаю, це була роса..."
            l "Або, я думаю, що я це зробив..."
            v "..."
            $ lisovyk_attitude-=10
            $ darkness_value+=10
            if darkness_value>=100:
                jump game_over_darkness
            # jump dim_screen
            jump lisovyk_remind_who_he_is

        "Пояснити, що він у пастці темряви":
            v "Ти забуваєш хто ти є через темряву, що нас огорнула."
            l "Я загублений, дитя."
            v "Скільки часу ти вже провів у цій глибині?"
            l "Не маю жодного уявлення, скільки це триває, і що саме сталося... Час летить!"
            l "Водночас, для мене час завжди летить інакше... Один рік для тебе – це як день для мене - це я пам’ятаю."
            l "Але цікаво, чому... Що я таке?"
            l "* задумався над своїм існуванням *"
            $ lisovyk_attitude+=10
            jump lisovyk_remind_who_he_is

    call screen backButton


label lisovyk_remind_who_he_is:
    v "Лісовик? Ти чуєш мене? Я можу допомогти тобі згадати хто ти є!"

    menu:
        # "Father of the foxes": #Cause 'FOX' is LYS
        "Father of the bananas":
            v "Лі-со-вик, ти – батько бананів."
            if lisovyk_banana_joke:
                l "Знову ти за свої банани, хехе."
                l "Я знаю, що мені подобаються банани – кому ж ні? – але це не зовсім про мене."
                l "Я сумую за моїми дорогими друзями..."
                v "* засоромилась через свої повторювані жарти *"
                $ lisovyk_attitude-= 10
                $ darkness_value+= 10
                if darkness_value>= 100:
                    jump game_over_darkness
                # jump dim_screen
                jump get_to_know_more
            else:
                l "Банани..? Я люблю всі рослини."
                l "Але найбільше – дерева. Знаєш, банан не є деревом, а травою?"
                v "Дерева, кущі, трава – порівняно зі мною, всі вони величезні! Давай сконцентруємося..."
                $ lisovyk_attitude -= 5
                $ darkness_value+= 5
                if darkness_value >= 100:
                    jump game_over_darkness
                # jump dim_screen
                jump get_to_know_more
        
        "Володар лісів":
            v "Лі-со-вик, ти - покровитель лісів."
            l "У лісах є дерева."
            l "На деревах є листя."
            l "Листя шелестить..."
            l "Це не голоси, а листя!"
            v "Саме так!"
            l "Про що шепоче листя, Віло?"
            v "Ти колись охороняв всі ліси і все, що в них живе!"
            l "А,хмм, це звучить знайомо..."
            v "Це місце зовсім не схоже на твій дім..."
            l "Чи є звідси вихід?"
            "Лісовик повертає голову ліворуч і праворуч, лунає скрип деревини..."
            #TODO sound of squeeku wood here
            $ lisovyk_attitude+= 10
            jump get_to_know_more

        "Покровитель лисих": #Cause BOLD is LYSYY
            v "Лі-со-вик, ти покровитель всіх лисих."
            l "Хе-хе. Лисих?"
            l "Моя одіж дійсно стає сухішою із кожним сезоном, і листя на моїй голові опадає, так..."
            l "...але це всього навсього природний процес."
            v "* упс *"
            $ lisovyk_attitude -= 10
            $ darkness_value+= 10
            if darkness_value >= 100:
                jump game_over_darkness
            # jump dim_screen
            jump get_to_know_more

        "Найкращий друг жаб":
            v "Lee-sooh-vyyyk, you are the friend of the frogs." # Cause he sits like a frog, and there's a small frog 
            v "Лі-со-вик, ти Друг жаб."
            l "Жаб, звичайно... Але не тільки жаб."
            l "Як я казав – я думаю, що казав – мені подобаються ~всі~ тварини."
            l "Я інколи сиджу, як жаба, по середах, бо так, проводжу з ними багато часу."
            l "* дивиться на свого друга-жабу *"
            #TODO frog sound
            $ lisovyk_attitude+=5 #seems too much, need to check if numbers make sense here!
            jump get_to_know_more

label get_to_know_more:
    menu:
        "Запитати про його минуле життя":
            v "Що ще ти пам'ятаєш, Лісовику?"
            if lisovyk_attitude >= 5:
                l "Пам'ятаю, що раніше все було по-інакшому, не так, як зараз."
                v "Я знаю, Лісовику... Мені шкода. Яким було твоє життя тоді?"
                l "Мій дім ділили ми з багатьма іншими створіннями – птахами, вовками, ведмедями..."
                l "Я ходив тими самим старими стежками щодня, дорожив спокоєм лісу, і захищав його."
                $ lisovyk_attitude += 5
            else:
                l "Я не пам'ятаю нічого, крім звуків."
                v "Що то були за звуки? Спів птахів? Стрибки зайців?"
                l "Останні спогади здаються темними, дуже темними. Згадую лише тріскання вогню, крики і перед очима дим, всюди дим..."
                l "Не можу згадати, яким життя було до того."
                v "Ох..."
                $ lisovyk_attitude -= 5

            jump invite_lisovyk

        "Розповісти про його минуле життя:":
            v "Ти любив відпочивати біля підніжжя лісу та грати в карти з друзями!"
            l "Ігри з друзями – ха. Щось таке чуже тут. Ти перший відвідувач, якого я мав за довгий час..."
            v "Це було твоє дозвілля. А в інший час ти дбав про худобу, щоб вона не заблукала в твоєму лісі..."
            v "Ти також допомагав людям, але часто лише якщо вони залишали смаколики."
            l "Ти починаєш мені подобатись! Що ти сама робиш тут?"
            v "Я допомагаю духам, як ми, втекти з темряви... Або, принаймні, я намагаюсь це зробити..."
            $ lisovyk_attitude+= 5
            jump invite_lisovyk

label invite_lisovyk:
    if lisovyk_attitude >= 20: #make the level harder?
        v "Я знаю, що звідси є шлях, шлях нагору, до сонця. Чи хочеш ти знову почути спів птахів і шелест листя? Хочеш приєднатися до мене?"

        show lisovyk radiant

        play sound tone
        $ lisovyk_joined = True
        if wing_strength < wing_strength_threshold:
            $ wing_strength += 1

            
        l "О, я зробив би все, щоб знову побачити свій ліс, щоб захищати його і його мешканців від небезпеки, що є досі над нами, і забезпечувати тінь у часи миру..."
        pause 0.5
        # play sound tone
    
    else:
        v "Я знаю, що звідси є шлях, шлях нагору, до сонця. Чи хочеш ти знову почути спів птахів і шелест листя? Хочеш приєднатися до мене?"

        l "Я не розумію, що ти хочеш від мене, дитя."
        l "Ти зробила мене ще більш непевним у тому, хто я є і ким я був.."
        l "Гадаю, я все одно скоро забуду про цю зустріч, брмсс."
        l "* засинає *"  

        $ increase_darkness()
    
    $ lisovyk_attitude = 0
    $ lisovyk_visited = True 

    call screen backButton
    
    