#Add a polunytsa joke

#ref: https://en.wikipedia.org/wiki/Lady_Midday

#Poludnitsa is a noon demon in Slavic mythology. She can be referred to in English as "Lady Midday", "Noonwraith" or "Noon Witch". 
#She was usually pictured as a young woman dressed in white that roamed field bounds.[3] She assailed folk working at noon, 
#causing heatstrokes and aches in the neck; sometimes she even caused madness.

#Poludnitsa, who makes herself evident in the middle of hot summer days, takes the form of whirling dust clouds
# and carries a scythe, sickle or shears; most likely the shears would be of an older style, not akin to modern scissors. 
#She will stop people in the field to ask them difficult questions or engage them in conversation. If anyone fails to answer 
#a question or tries to change the subject, she will cut off their head or strike them with illness. She may appear as an old hag,
# a beautiful woman, or a 12-year-old girl, and she was useful in scaring children away from valuable crops. 
#She is only seen on the hottest part of the day and is a personification of a sun-stroke.[5]

#Poludnitsa, according to beliefs, loves to dance. If she sees a girl lying down to rest in the field, she will wake her 
#up and begin to persuade her to dance. If the girl agrees, she will be forced to dance until the Â«evening dawnÂ». 
#Poludnitsa cannot be beaten in dancing; however, if such a girl is found, the noon spirit will present her with a rich dowry.[8]

#ref: https://uk.wikipedia.org/wiki/%D0%9F%D0%BE%D0%BB%D1%83%D0%B4%D0%BD%D0%B8%D1%86%D1%8F
# Ð—Ð°Ð³Ð°Ð»ÑŒÐ½Ðµ Ð·Ð°Ð½ÑÑ‚Ñ‚Ñ Ð¿Ð¾Ð»ÑƒÐ´Ð½Ð¸Ñ†ÑŒ â€” ÐºÐ°Ñ€Ð°Ñ‚Ð¸ Ñ‚Ð¸Ñ…, Ñ…Ñ‚Ð¾ Ð¿Ñ€Ð°Ñ†ÑŽÑŽÑ‚ÑŒ Ñƒ Ð¿Ð¾Ð»Ñ– Ð² Ð¿Ð¾Ð»ÑƒÐ´ÐµÐ½ÑŒ. Ð›ÑŽÐ´ÑÐ¼, ÑÐºÐ¸Ñ… Ð²Ð¾Ð½Ð¸ Ð·ÑƒÑÑ‚Ñ€Ñ–Ñ‡Ð°Ð»Ð¸ Ð½Ð° Ð¿Ð¾Ð»Ñ–,
# Ð·Ð°Ð³Ð°Ð´ÑƒÐ²Ð°Ð»Ð¸ Ð·Ð°Ð³Ð°Ð´ÐºÐ¸, ÑÐºÑ– Ð½ÐµÐ¼Ð¾Ð¶Ð»Ð¸Ð²Ð¾ Ð²Ñ–Ð´Ð³Ð°Ð´Ð°Ñ‚Ð¸[2], Ñ– Ð·Ð° Ð²Ñ–Ð´Ð¿Ð¾Ð²Ñ–Ð´ÑÐ¼Ð¸ Ð²Ð¸Ñ€Ñ–ÑˆÑƒÐ²Ð°Ð»Ð¸ Ð²Ð±Ð¸Ñ‚Ð¸ Ñ‡Ð¸ ÑÐºÐ°Ð»Ñ–Ñ‡Ð¸Ñ‚Ð¸ ÑÐ²Ð¾Ñ—Ñ… Ð¶ÐµÑ€Ñ‚Ð². 
#Ð¢Ð°ÐºÐ¾Ð¶ Ð´ÑƒÑˆÐ¸Ð»Ð¸ Ð¶ÐµÐ½Ñ†Ñ–Ð², ÑÐºÑ– ÑÐ¿Ð°Ð»Ð¸ Ð² Ð¿Ð¾Ð»Ñ–, Ð²Ð¸ÐºÑ€Ð°Ð´Ð°Ð»Ð¸ Ð´Ñ–Ñ‚ÐµÐ¹, ÑÐºÑ– Ð³Ñ€Ð°Ð»Ð¸ÑÑ Ð½Ð° ÐºÑ€Ð°ÑŽ Ð¿Ð¾Ð»Ñ[4], Ð¿Ð¾ÐºÐ¸Ð´Ð°Ð»Ð¸ Ð´Ñ–Ð¼ Ñ‡Ð¸ ÐºÑ€Ð°Ð»Ð¸ Ð¿Ð»Ð¾Ð´Ð¸. 
#Ð§Ð°ÑÐ¾Ð¼ Ð±Ð¸Ð»Ð¸ Ð¿Ð¾ Ð½Ð¾Ð³Ð°Ñ… Ñ‚Ð¸Ñ…, Ñ…Ñ‚Ð¾ Ñ—Ð¼ Ð½Ðµ Ð²ÐºÐ»Ð¾Ð½ÑÑ”Ñ‚ÑŒÑÑ[2][3]. Ð’ Ð£ÐºÑ€Ð°Ñ—Ð½Ñ–, Ð½Ð° Ð–Ð¸Ñ‚Ð¾Ð¼Ð¸Ñ€Ñ‰Ð¸Ð½Ñ–, Ð²Ñ–Ñ€Ð¸Ð»Ð¸, Ñ‰Ð¾ Ð¿Ð¾Ð»ÑƒÐ´Ð½Ð¸Ñ†Ñ Ð·Ð¼ÑƒÑˆÑƒÑ” Ð»ÑŽÐ´Ð¸Ð½Ñƒ
# Ð±Ð»ÑƒÐºÐ°Ñ‚Ð¸ Ð¿Ð¾ Ð»Ñ–ÑÑƒ Ð² Ð¿Ð¾Ð»ÑƒÐ´ÐµÐ½ÑŒ[6].

#Ð›ÑƒÐ¶Ð¸Ñ‡Ð°Ð½Ð¸, Ð¿Ñ€Ð¾Ñ‚Ðµ, Ð½Ð°Ð´Ñ–Ð»ÑÐ»Ð¸ Ð¿Ð¾Ð»ÑƒÐ´Ð½Ð¸Ñ†ÑŽ Ñ– ÐºÐ¾Ñ€Ð¸ÑÐ½Ð¸Ð¼Ð¸ Ñ„ÑƒÐ½ÐºÑ†Ñ–ÑÐ¼Ð¸. Ð’Ð¾Ð½Ð¸ Ð²Ð²Ð°Ð¶Ð°Ð»Ð¸, Ñ‰Ð¾ Ð¿Ð¾Ð»ÑƒÐ´Ð½Ð¸Ñ†Ñ ÐºÑ€Ñ–Ð¼ Ñ‚Ð¾Ð³Ð¾ Ð¾Ñ…Ð¾Ñ€Ð¾Ð½ÑÑ” Ð¿Ð¾Ð»Ðµ Ð²Ñ–Ð´ Ð·Ð»Ð¾Ð´Ñ–Ñ—Ð²[2],
# ÐºÐ¾ÑÐ¸Ñ‚ÑŒ Ð¿Ð¾Ð»Ðµ Ð·Ð°Ð¼Ñ–ÑÑ‚ÑŒ Ð»ÑŽÐ´ÐµÐ¹[8]; Ð²Ñ–Ñ€Ð¸Ð»Ð¸, Ñ‰Ð¾ Ñ…Ñ‚Ð¾ Ð½Ð°ÑÐ¼Ñ–Ñ…Ð°Ñ‚Ð¸Ð¼ÐµÑ‚ÑŒÑÑ Ð· Ð¿Ð¾Ñ‚Ð²Ð¾Ñ€Ð½Ð¾Ñ— Ð¿Ð¾Ð»ÑƒÐ´Ð½Ð¸Ñ†Ñ–, Ñ‚Ð¾Ð¼Ñƒ Ð²Ð¾Ð½Ð° Ð½Ð°ÑˆÐ»Ðµ Ð²Ð¸Ñ€Ð°Ð·ÐºÐ¸, Ð°Ð»Ðµ Ñ…Ñ‚Ð¾ Ñ—Ñ— 
#Ð¿Ð¾Ñ…Ð²Ð°Ð»Ð¸Ñ‚ÑŒ Ð°Ð±Ð¾ Ñ‰Ð¾ÑÑŒ Ð¿Ð¾Ð´Ð°Ñ€ÑƒÑ” â€” Ð¾Ñ‚Ñ€Ð¸Ð¼Ð°Ñ” Ð²Ñ–Ð´ Ð¿Ð¾Ð»ÑƒÐ´Ð½Ð¸Ñ†Ñ– Ð½ÐµÑÐºÑ–Ð½Ñ‡ÐµÐ½Ð½Ð¸Ð¹ ÐºÐ»ÑƒÐ±Ð¾Ðº Ð¿Ñ€ÑÐ¶Ñ–[9]. Ð¯Ðº Ð¾Ñ…Ð¾Ñ€Ð¾Ð½Ð½Ð¸Ñ†Ñ Ð¿Ð¾Ð»Ñ–Ð², Ð²Ð¾Ð½Ð° Ð¼Ð¾Ð³Ð»Ð° Ð²Ð¸Ð¿Ñ€Ð¾Ð±Ð¾Ð²ÑƒÐ²Ð°Ñ‚Ð¸
#Ð¶ÐµÐ½Ñ†Ñ–Ð², Ñ€Ð¾Ð·Ð¿Ð¸Ñ‚ÑƒÑŽÑ‡Ð¸ Ñ—Ñ… ÑÐº Ñ€Ð¾ÑÑ‚Ð¸Ñ‚Ð¸ Ð·Ð±Ñ–Ð¶Ð¶Ñ, Ñ‚Ð° ÐºÐ°Ñ€Ð°Ð»Ð° Ñ‚Ð¸Ñ…, Ñ…Ñ‚Ð¾ Ð½Ðµ Ð´Ð°Ð²Ð°Ð»Ð¸ Ð¿Ñ€Ð°Ð²Ð¸Ð»ÑŒÐ½Ð¾Ñ— Ð²Ñ–Ð´Ð¿Ð¾Ð²Ñ–Ð´Ñ–[10].



label room2:
    # scene room hell bg

    default poludnicia_attitude = 0 
    default poludnicia_visited = False
    default poludnicia_joined = False

    if poludnicia_joined:
        jump poludnicia_already_joined  
    elif poludnicia_visited:
        jump poludnicia_already_visited  
    else: 
        jump poludnicia_intro

label poludnicia_already_joined:
    b "*Looks at tumbleweed passing*"
    call screen backButton

label poludnicia_already_visited:
    show poludnicia at center with dissolve
    show berehynia at right with dissolve
    # b "Poludnytsa is not in the mood indeed. But you dealt with Mara, so this shoudl be doable too!"
    b "You dealt with my sister already, so persuading Poludnytsa would be like snacking on sunflower seeds..." #ÑÐº Ð½Ð°ÑÑ–ÌÐ½Ð½Ñ Ð»ÑƒÐ·Ð°ÌÑ‚Ð¸

    show vila at left with dissolve
    hide berehynia at right with dissolve
    show poludnicia at rightly with move 
    
    jump poludnicia_greet


label poludnicia_intro:
    show poludnicia at center with dissolve
    show vila at left with dissolve
    pause 0.1
    show berehynia at rightly with dissolve

    b "Poludnytsa is a spirit living in the fields. She may have a strong character - don't we all?"
    b "After all it is quite a toll to protect the fields...and unwanted visitors from being there at a wrong time."
    b "But now, you know, who wouldn't be angry from everything that happened to us? May luck be with you."


    hide berehynia at rightly with dissolve
    show poludnicia at rightly with move 

    p "This darkness is driving me mad!"
    p "It was ME who was driving others mad. Not vice versa!"
    p "I cannot even threaten anyone with... whatever I used to threaten with!" # (the power of the sun? the scythe?)
    p "* walks angrilly back and forth * "

    jump poludnicia_greet



label poludnicia_greet: 
    menu:
        "Greet gently":
            v "Hi..."
            p "*still preoccupied with her emptions*"
            v "Poludnytsa, hello. I... I am Vila."
            p "I had met enough villains in my life! No, thank you." #TODO adapt for uktrainian maybe, smth like Ð’Ð¸Ð»Ð¸ - Ð´Ð»Ñ Ð¿Ð¾Ð»Ñ?
            v "Vi-la. Not a villain."
            p "What do you want, 'Viii-laaa'?"
            $ poludnicia_attitude+=5
            jump poludnicia_remind
            

        "Tell to calm down":
            v "Woah woah woah, someone needs to chill here..."
            p "Chill?!"
            p "Are you kidding me?! Look where we are! And besides..."
            p "..who, in Mara's name, are you?"
            v "Khmm, I am Vila.. I just recently realized the state of things myself!"
            v "You must believe me."
            p "?!"
            $ poludnicia_attitude-=10
            $ darkness_value+= 10
            if darkness_value>= 100:
                jump game_over_darkness
            # jump dim_screen
            jump poludnicia_remind
            

        "Wait quietly":
            v "..."
            show poludnicia at center with move 
            p "*continues with anger*"
            show poludnicia at rightly with move 
            v "..."
            p "You're not triggered by my anger?"
            p "How are you staying so calm?"
            p "I haven't experienced calmness in ages!"
            v "..."
            p "Hm, I like you.. kind of. What is your name?"
            v "I am Vila.. Hello Polydnytsa!"
            $ poludnicia_attitude+=10
            jump poludnicia_remind

            
label poludnicia_remind:
    v "I can help you remember who you are!"
    
    menu:
        "The sun master":
            v "You control the sun going up and down over our villages."
            v "Thus, it hits the fields ~exactly~ how it needs to, at Midday!"
            p "I do remember being more active at the hottest hour of the hottest summer days!"
            p "I don't control its movement though. His name is Dazhbog, or something."
            p "So, you must be mistaken."
            v "*fearful inhale*"
            p "Good try! .. What sense talking of the sun, when we are stuck in this freaking cave!!!"
            $ poludnicia_attitude-=5
            $ darkness_value+= 10
            if darkness_value>= 100:
                jump game_over_darkness
            # jump dim_screen
            jump poludnicia_reassure

        "The field goddess":
            v "You are the goddess of the fields and protect them from the thieves."
            p "How come you know me, but I don't know you at all?!"
            p "Anywho, it does hit home."
            p "For most of the time, I remember, I used to fly around, disquised as dust carried by the wind."
            p "And then I come as myself..."
            v "...at Midday."
            p "When the sun is at its highest. High time for me.."
            v "..No work for others."
            p "Exactly! Good times... The times that are gone now!"
            $ poludnicia_attitude+=10
            jump poludnicia_reassure

        "The scarecrow woman":
            v "Essentially, you scare away any visitors of fields at Midday. So I've heard."
            v "You are known to protect the fields, but your methods are, hmm, pretty rough, to put midly."
            p "'Scarecrow'... This is a bold statement, Vila!!"
            v "*oh-oh*"
            p "I like this compliment! Feels like true me."
            p "NO WORK AT MIDDAY - is it so hard to follow that rule?! They keep coming and coming. The small ones, the big ones. Someone needs to stop it."
            v "That's why you use ... the scythe or a sickle?"
            p "Ha, this is outdated. Now it's just about fashion."
            p "What I do now is, ask reapers difficult questions. The ones, like how to sow, and when to reap? You know, the field stuff."
            v "And if they don't answer?"
            p "That you don't wanna know..."
            p "There are worse things happenig now. Field was no safe place for humans at midday. Well now, for nobody.. even me."
            # reapers in the field - ask them questions about how to take care of the fields.
            $ poludnicia_attitude+=5
            jump poludnicia_reassure


label poludnicia_reassure:
    v "I feel your frustartion, Poludnytsa. Things are not easy now indeed... "
    v "So..."
    # v "Would you want to do some of your favourite activities?"
    menu:
        "Let's dance!":
            # ref: "P"oludnitsa, according to beliefs, loves to dance. If she sees a girl lying down to rest in the field, she will wake her 
            #up and begin to persuade her to dance. If the girl agrees, she will be forced to dance until the Â«evening dawnÂ». 
            #Poludnitsa cannot be beaten in dancing; however, if such a girl is found, the noon spirit will present her with a rich dowry.[8]"

            v "Do you hear the wind howling? What a beat!"
            v "*starts dancing*"
            show vila at rightly with move 
            show poludnicia at left with move 
            show vila at left with move 
            show poludnicia at rightly with move 
            p "* a moment of shock*"
            # show poludnicia at left with move 
            # show poludnicia at rightly with move 
            p "I don't remember the last time I danced!!!"
            p "I will not force you to dance till the dawn, like we would usually. We don't even know when the dawn comes deep in this darkness."
            p "*Poludnytsa looks up*"
            $ poludnicia_attitude+=10
            jump poludnicia_invite

        "Let's ask difficult questions!": 
            # ref "She will stop people in the field to ask them difficult questions or engage them in conversation. 
            # If anyone fails to answer a question or tries to change the subject, she will cut off their head or strike 
            # them with illness."
            v "Just like you ask reapers difficult questions, could I ask you one?"
            p "Noot sure I like the reversed game, noone dared yet to do it.. But go on!"
            v "Say 'Polunytsa'."
            p "'Polunytsa'? Why?"
            v "That's the answer."
            v "*smiles*"
            p "Ha, that was too easy!!"
            p "Your turn!"
            p "Can you tell me.. Where is Zhytomyr?"
            v "ðŸ‘€"
            p "One of my favourites. I wonder what's up with it now?"
            p "I wish I could revenge for it! If needed, of course... If there is anything to revenge for still?"
            $ poludnicia_attitude+=5 
            jump poludnicia_invite

        "Let's fence with sickles!": #pretend her powers are still with her
            v "If your sickle has no good use now... Then how about we play with it?"
            v "I'll be the reaper, and you play yourself!"
            p "Outrageous! Sickles are not toys!"
            p "And I thought I was the one driven mad by the darkness!"
            $ poludnicia_attitude-=5 
            $ darkness_value+= 10
            if darkness_value>= 100:
                jump game_over_darkness
            # jump dim_screen
            jump poludnicia_invite

label poludnicia_invite:
    if poludnicia_attitude>=15:
        v "Poludnytsa, I know there is way out of here. There will be the sun, and hot summer days, there will be new fields to watch. Do you want to come along?"
        p "If I am mad, then this is part of it, and I lose nothing."
        p "I want to let my anger go, I want to fly, and fly, and scream, till everyone can hear me. I'll join you, Vila!"

        "TODO show radiant Poludnytsa"
        $ wing_strength+=1
        jump map

    else:
        v "Poludnytsa, I know there is way out of here. There will be the sun, and hot summer days, there will be new fields to watch. Do you want to come along?"
        p "I see no point in going anywhere!"
        p "These are empty promises, it seems. The past me is the past, whatever she was like..."
        v "Poludnytsa..."
        $ increase_darkness()

    $ poludnicia_attitude = 0
    $ poludnicia_visited = True 

    call screen backButton 
